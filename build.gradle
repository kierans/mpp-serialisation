plugins {
    id "org.jetbrains.kotlin.multiplatform" version "1.6.20"
    id "org.jetbrains.kotlin.plugin.serialization" version "1.6.20"
    id "io.kotest.multiplatform" version "5.0.2"
}

group "org.example"
version "1.0-SNAPSHOT"

ext {
    arrowVersion = "1.0.1"
    kotlinCoroutinesVersion = "1.6.0"
    kotestVersion = "5.1.0"
}

repositories {
    mavenCentral()
}

kotlin {
    /* Targets configuration omitted.
    *  To find out how to configure the targets, please follow the link:
    *  https://kotlinlang.org/docs/reference/building-mpp-with-gradle.html#setting-up-targets */

    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = "1.8"
        }
        withJava()
        testRuns["test"].executionTask.configure {
            useJUnitPlatform()
        }
    }

    ios {
        binaries {
            framework {
                baseName = "IosNumbers"
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                api "org.jetbrains.kotlinx:kotlinx-serialization-json:1.3.2"

                implementation kotlin("stdlib-common")
            }
        }

        commonTest {
            dependencies {
                implementation "io.kotest:kotest-framework-engine:${rootProject.ext.kotestVersion}"
                implementation "io.kotest:kotest-assertions-core:${rootProject.ext.kotestVersion}"
                implementation "io.kotest:kotest-property:${rootProject.ext.kotestVersion}"
                implementation "io.kotest.extensions:kotest-assertions-arrow:1.2.3"
            }
        }

        iosMain {
            dependsOn(commonMain)
            iosX64Main.dependsOn(it)
            iosArm64Main.dependsOn(it)
        }

        iosTest {
            dependsOn(commonTest)
            iosX64Test.dependsOn(it)
            iosArm64Test.dependsOn(it)
        }

        jvmTest {
            dependencies {
                implementation "io.kotest:kotest-runner-junit5:${rootProject.ext.kotestVersion}"
            }
        }
    }
}
